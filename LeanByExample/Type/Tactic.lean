/- # Tactic

`Lean.Elab.Tactic.Tactic` å‹ã®é …ã¯ã€ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã®å†…éƒ¨å®Ÿè£…ã‚’è¡¨ç¾ã—ã¦ã„ã¾ã™ã€‚ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã¨ã¯è¨¼æ˜ã®çŠ¶æ…‹ã‚’æ“ä½œã™ã‚‹é–¢æ•°ã§ã‚ã‚Šã€`Tactic` å‹ã¯ `Syntax â†’ TacticM Unit` ã¨ã„ã†é–¢æ•°å‹ãã®ã‚‚ã®ã§ã™ã€‚
-/
import Lean
import Qq --#
import Batteries --#

example : Lean.Elab.Tactic.Tactic = (Lean.Syntax â†’ Lean.Elab.Tactic.TacticM Unit) := by rfl

/- ## Tactic å‹ã‹ã‚‰ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã‚’ä½œã‚‹

`Tactic` å‹ã®é …ã‹ã‚‰ã¯ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã‚’å®šç¾©ã™ã‚‹ã“ã¨ãŒã§ãã¾ã™ã€‚-/
/- ### tada ã§è¨¼æ˜çµ‚äº†ã‚’ãŠç¥ã„ã™ã‚‹ã‚¿ã‚¯ãƒ†ã‚£ã‚¯

[`done`](#{root}/Tactic/Done.md) ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã®æ´¾ç”Ÿã¨ã—ã¦ã€ã‚´ãƒ¼ãƒ«ãŒãªããªã£ãŸã‚‰ ğŸ‰ ã§ãŠç¥ã„ã™ã‚‹ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã‚’ä½œã‚‹ã“ã¨ãŒã§ãã¾ã™ã€‚
-/
syntax (name := tada) "tada" : tactic

open Lean Elab Tactic Term in

@[tactic tada]
def evalTada : Tactic := fun _stx => do
  -- ç¾åœ¨ã®æœªè§£æ±ºã®ã‚´ãƒ¼ãƒ«ã‚’å–å¾—ã™ã‚‹
  let goals â† getUnsolvedGoals

  -- æœªè§£æ±ºã®ã‚´ãƒ¼ãƒ«ãŒã‚ã‚‹å ´åˆ
  unless goals.isEmpty do
    reportUnsolvedGoals goals
    throwAbortCommand

  logInfo "ğŸ‰ ãŠã‚ã§ã¨ã†ã”ã–ã„ã¾ã™ï¼è¨¼æ˜å®Œäº†ã§ã™ï¼"

/-â‹†-//-- info: ğŸ‰ ãŠã‚ã§ã¨ã†ã”ã–ã„ã¾ã™ï¼è¨¼æ˜å®Œäº†ã§ã™ï¼ -/
#guard_msgs in --#
example : True := by
  trivial
  tada

/- ### trivial ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã®åˆ¶é™ç‰ˆ

[`trivial`](#{root}/Tactic/Trivial.md) ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã®æ©Ÿèƒ½ã‚’åˆ¶é™ã—ã€`True` ã¨ã„ã†ã‚´ãƒ¼ãƒ«ã‚’é–‰ã˜ã‚‹æ©Ÿèƒ½ã ã‘ã‚’æŒã¤ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã‚’æ§‹æˆã™ã‚‹ã“ã¨ãŒã§ãã¾ã™ã€‚[^trivial]
-/

syntax (name := myTrivial) "my_trivial" : tactic

open Lean Elab Tactic Qq in

@[tactic myTrivial]
def evalMyTrivial : Tactic := fun _stx => do
  -- ç¾åœ¨ã®ã‚´ãƒ¼ãƒ«ã‚’å–å¾—ã™ã‚‹
  let goal : MVarId â† getMainGoal
  try
    -- ã‚´ãƒ¼ãƒ«ãŒ `True.intro` ã§é–‰ã˜ã‚‰ã‚Œã‚‹ã‹è©¦ã™
    goal.assignIfDefEq q(True.intro)
  catch _error =>
    -- å¤±æ•—ã—ãŸå ´åˆã¯ã‚´ãƒ¼ãƒ«ã®å‹ã‚’å–å¾—ã—ã¦ã‚¨ãƒ©ãƒ¼ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ã‚’è¡¨ç¤ºã™ã‚‹
    let goalType â† goal.getType
    throwError "my_trivialã‚¿ã‚¯ãƒ†ã‚£ã‚¯ãŒå¤±æ•—ã—ã¾ã—ãŸã€‚ã‚´ãƒ¼ãƒ«ã®å‹ã¯`{goalType}`ã§ã‚ã£ã¦`True`ã§ã¯ã‚ã‚Šã¾ã›ã‚“ã€‚"

example : True := by
  my_trivial

/-â‹†-//-- error: my_trivialã‚¿ã‚¯ãƒ†ã‚£ã‚¯ãŒå¤±æ•—ã—ã¾ã—ãŸã€‚ã‚´ãƒ¼ãƒ«ã®å‹ã¯`False`ã§ã‚ã£ã¦`True`ã§ã¯ã‚ã‚Šã¾ã›ã‚“ã€‚ -/
#guard_msgs in --#
example : False := by
  my_trivial

/- ### assumption ã‚¿ã‚¯ãƒ†ã‚£ã‚¯

[`assumption`](#{root}/Tactic/Assumption.md) ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã®ã‚ˆã†ã«ã€ã‚´ãƒ¼ãƒ«ã®è¨¼æ˜ãŒæ—¢ã«ä»®å®šã«ã‚ã‚‹ã¨ãã«ã‚´ãƒ¼ãƒ«ã‚’é–‰ã˜ã‚‹ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã¯æ¬¡ã®ã‚ˆã†ã« `Tactic` å‹ã®é–¢æ•°ã«ã‚ˆã£ã¦å®Ÿè£…ã§ãã¾ã™ã€‚
-/

syntax (name := myAssumption) "my_assumption" : tactic

open Lean Elab Tactic in

@[tactic myAssumption]
def evalMyAssumption : Tactic := fun _stx => withMainContext do
  -- ç¾åœ¨ã®ã‚´ãƒ¼ãƒ«ã¨ãƒ­ãƒ¼ã‚«ãƒ«ã‚³ãƒ³ãƒ†ã‚­ã‚¹ãƒˆã‚’å–å¾—ã™ã‚‹
  let goal â† getMainGoal
  let ctx â† getLCtx

  for (decl : LocalDecl) in ctx do
    -- ãƒ­ãƒ¼ã‚«ãƒ«å®£è¨€ã®ç¨®é¡ãŒãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã§ãªã„å ´åˆã¯ã‚¹ã‚­ãƒƒãƒ—ã™ã‚‹
    if decl.kind != .default then
      continue
    try
      -- ã‚´ãƒ¼ãƒ«ã«ãƒ­ãƒ¼ã‚«ãƒ«å®£è¨€ã®å¤‰æ•°ã‚’ä»£å…¥ã™ã‚‹
      goal.assignIfDefEq decl.toExpr
      -- æˆåŠŸã—ãŸã‚‰çµ‚äº†
      return
    catch _ =>
      -- å¤±æ•—ã—ã¦ã‚‚ç„¡è¦–ã—ã¦æ¬¡ã®å€™è£œã«é€²ã‚€
      pure ()
  throwError "my_assumptionã‚¿ã‚¯ãƒ†ã‚£ã‚¯ãŒå¤±æ•—ã—ã¾ã—ãŸã€‚"

example {P : Prop} (hP : P) : P := by
  my_assumption

/-â‹†-//-- error: my_assumptionã‚¿ã‚¯ãƒ†ã‚£ã‚¯ãŒå¤±æ•—ã—ã¾ã—ãŸã€‚ -/
#guard_msgs in --#
example {P Q : Prop} (hP : P) : Q := by
  my_assumption

/- ### And å°‚ç”¨ constructor

[`constructor`](#{root}/Tactic/Constructor.md) ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã®æ©Ÿèƒ½ã‚’åˆ¶é™ã—ã€`And` å‹ã®ã‚´ãƒ¼ãƒ«ã‚’åˆ†å‰²ã™ã‚‹æ©Ÿèƒ½ã ã‘ã‚’æŒã¤ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã‚’æ§‹æˆã™ã‚‹ä¾‹ã‚’ç¤ºã—ã¾ã™ã€‚[^and_constructor]
-/
section --#

/-- ã‚´ãƒ¼ãƒ«ãŒ`P âˆ§ Q`ã¨ã„ã†å½¢ã‚’ã—ã¦ã„ãŸã‚‰ã€åˆ†è§£ã—ã¦ãã‚Œãã‚Œåˆ¥ã‚´ãƒ¼ãƒ«ã«ã™ã‚‹ -/
syntax (name := andConstructor) "and_constructor" : tactic

open Lean Elab Tactic Qq Meta

/-- ã‚´ãƒ¼ãƒ«ãŒ `P âˆ§ Q` ã®å½¢ã‚’ã—ã¦ã„ã‚‹ã‹ãƒã‚§ãƒƒã‚¯ã—ã¦ã€
`P âˆ§ Q` ã®å½¢ã‚’ã—ã¦ã„ãŸã‚‰ `P` ã¨ `Q` ã‚’ãã‚Œãã‚Œè¿”ã™ -/
def extracetAndGoals : TacticM (Q(Prop) Ã— Q(Prop)) := do
  have tgt : Q(Prop) := â† getMainTarget -- å³è¾ºã§Qqã‚’ä½¿ç”¨ã—ã¦ã„ãªã„ã®ã§haveã‚’ä½¿ã†
  match tgt with
  | ~q($p âˆ§ $q) => return (p, q)
  | _ => throwError "ã‚´ãƒ¼ãƒ«ãŒ `P âˆ§ Q` ã®å½¢ã§ã¯ã‚ã‚Šã¾ã›ã‚“ã€‚"

@[tactic andConstructor]
def evalAndConstructor : Tactic := fun _stx => withMainContext do
  -- ã‚´ãƒ¼ãƒ«ã‚’å–å¾—ã™ã‚‹
  let goal â† getMainGoal
  have (p, q) := â† extracetAndGoals

  -- æ–°ã—ã„ãƒ¡ã‚¿å¤‰æ•°ï¼ˆã‚´ãƒ¼ãƒ«ï¼‰ã‚’ä½œæˆã™ã‚‹
  have left : Q($p) := â† mkFreshExprSyntheticOpaqueMVar p (tag := `left)
  have right : Q($q) := â† mkFreshExprSyntheticOpaqueMVar q (tag := `right)

  -- ã‚´ãƒ¼ãƒ«ã‚’`?_ âˆ§ ?_`ã®å½¢ã«ã¯ã‚ã‚‹
  goal.assign q(And.intro $left $right)

  -- ã‚¢ã‚¯ãƒ†ã‚£ãƒ–ãªã‚´ãƒ¼ãƒ«ã®ãƒªã‚¹ãƒˆã¯è‡ªå‹•çš„ã«ã¯æ›´æ–°ã•ã‚Œãªã„ã®ã§ã€
  -- ï¼’ã¤ã®ã‚´ãƒ¼ãƒ«ã‚’ä½œã£ãŸã“ã¨ã‚’å®£è¨€ã™ã‚‹
  replaceMainGoal [left.mvarId!, right.mvarId!]

example : True âˆ§ True := by
  and_constructor
  Â· trivial
  Â· trivial

end --#
/- ### Iff å°‚ç”¨ constructor

[`constructor`](#{root}/Tactic/Constructor.md) ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã®æ©Ÿèƒ½ã‚’åˆ¶é™ã—ã€`P â†” Q` ã¨ã„ã†å½¢ã®ã‚´ãƒ¼ãƒ«ã‚’åˆ†è§£ã™ã‚‹æ©Ÿèƒ½ã ã‘ã‚’æŒã¤ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã‚’æ§‹æˆã™ã‚‹ä¾‹ã‚’ç¤ºã—ã¾ã™ã€‚[^iff_constructor]
-/
section --#

/-- ã‚´ãƒ¼ãƒ«ãŒ`P â†” Q`ã¨ã„ã†å½¢ã‚’ã—ã¦ã„ãŸã‚‰`P â†’ Q`ã¨`Q â†’ P`ã¨ã„ã†äºŒã¤ã®ã‚´ãƒ¼ãƒ«ã«åˆ†è§£ã™ã‚‹ -/
syntax (name := iffConstructor) "iff_constructor" : tactic

open Lean Elab Tactic Qq Meta

/-- ã‚´ãƒ¼ãƒ«ãŒ `P â†” Q` ã®å½¢ã‚’ã—ã¦ã„ã‚‹ã‹ãƒã‚§ãƒƒã‚¯ã—ã¦ã€
`P â†” Q` ã®å½¢ã‚’ã—ã¦ã„ãŸã‚‰ `P` ã¨ `Q` ã‚’ãã‚Œãã‚Œè¿”ã™ -/
def extractIffGoals : TacticM (Q(Prop) Ã— Q(Prop)) := do
  have tgt : Q(Prop) := â† getMainTarget -- å³è¾ºã§Qqã‚’ä½¿ç”¨ã—ã¦ã„ãªã„ã®ã§haveã‚’ä½¿ã†
  match tgt with
  | ~q($p â†” $q) => return (p, q)
  | _ => throwError "ã‚´ãƒ¼ãƒ«ãŒ `P â†” Q` ã®å½¢ã§ã¯ã‚ã‚Šã¾ã›ã‚“ã€‚"

@[tactic iffConstructor]
def evalIffConstructor : Tactic := fun _stx => withMainContext do
  -- ã‚´ãƒ¼ãƒ«ã‚’å–å¾—ã™ã‚‹
  let goal â† getMainGoal
  have (p, q) := â† extractIffGoals

  -- æ–°ã—ã„ãƒ¡ã‚¿å¤‰æ•°ï¼ˆã‚´ãƒ¼ãƒ«ï¼‰ã‚’ä½œæˆã™ã‚‹
  have mp : Q($p â†’ $q) := â† mkFreshExprSyntheticOpaqueMVar q($p â†’ $q) (tag := `mp)
  have mpr : Q($q â†’ $p) := â† mkFreshExprSyntheticOpaqueMVar q($q â†’ $p) (tag := `mpr)

  -- ã‚´ãƒ¼ãƒ«ã‚’`?_ â†” ?_`ã®å½¢ã«ã¯ã‚ã‚‹
  goal.assign q(Iff.intro $mp $mpr)

  -- ã‚¢ã‚¯ãƒ†ã‚£ãƒ–ãªã‚´ãƒ¼ãƒ«ã®ãƒªã‚¹ãƒˆã¯è‡ªå‹•çš„ã«ã¯æ›´æ–°ã•ã‚Œãªã„ã®ã§ã€
  -- ï¼’ã¤ã®ã‚´ãƒ¼ãƒ«ã‚’ä½œã£ãŸã“ã¨ã‚’å®£è¨€ã™ã‚‹
  replaceMainGoal [mp.mvarId!, mpr.mvarId!]

example : True â†” True := by
  iff_constructor
  Â· simp
  Â· simp

end --#
/- ### `Aâ‚ âˆ§ Aâ‚‚ âˆ§ ... âˆ§ Aâ‚™` ã¨ã„ã†å½¢ã®å‰æã‹ã‚‰ `âŠ¢ Aáµ¢` ã‚’ç¤ºã™ã‚¿ã‚¯ãƒ†ã‚£ã‚¯
`h : Aâ‚ âˆ§ Aâ‚‚ âˆ§ ... âˆ§ Aâ‚™` ã¨ã„ã†å½¢ã®å‰æã‹ã‚‰ `âŠ¢ Aáµ¢` ã‚’ç¤ºã™ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã‚’å®Ÿè£…ã™ã‚‹ä¾‹ã‚’ç¤ºã—ã¾ã™ã€‚ã“ã‚Œã¯å¼•æ•°ã‚’æŒã¤ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã®ä¾‹ã§ã‚ã‚‹ã¨ã¨ã‚‚ã«ã€å†å¸°çš„ãªæŒ™å‹•ã‚’ã™ã‚‹ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã®ä¾‹ã§ã‚‚ã‚ã‚Šã¾ã™ã€‚[^destruct_and]
-/
section --#

/-- `Aâ‚ âˆ§ Aâ‚‚ âˆ§ ... âˆ§ Aâ‚™` ã¨ã„ã†å½¢ã®å‰æã‹ã‚‰ `âŠ¢ Aáµ¢` ã‚’ç¤ºã™ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ -/
syntax (name := destructAnd) "destruct_and" ident : tactic

open Lean Elab Tactic Qq Meta

/-- è¨¼æ˜é … `hp : Q` ãŒ `Aâ‚ âˆ§ Aâ‚‚ âˆ§ ... âˆ§ Aâ‚™` ã®å½¢ã®å‘½é¡Œã®è¨¼æ˜ã§ã‚ã‚‹ã‹ãƒã‚§ãƒƒã‚¯ã—ã¦ã€
å†å¸°çš„ã«åˆ†è§£ã—ã¦ç¾åœ¨ã®ã‚´ãƒ¼ãƒ«ã¨ä¸€è‡´ã™ã‚‹è¨¼æ˜ãŒå¾—ã‚‰ã‚Œã‚‹ã‹ã‚’ç¢ºèªã—ã€
ã‚‚ã—ä¸€è‡´ã™ã‚Œã°ã‚´ãƒ¼ãƒ«ã‚’é–‰ã˜ã¦`true`ã‚’è¿”ã™ã€‚ä¸€è‡´ã—ãªã‘ã‚Œã°`false`ã‚’è¿”ã™ã€‚ -/
partial def destructAndExpr (P : Q(Prop)) (hp : Q($P)) : TacticM Bool := withMainContext do
  -- ä»Šè¨¼æ˜ã‚’æ§‹æˆã—ã‚ˆã†ã¨ã—ã¦ã„ã‚‹å‘½é¡Œã‚’å–å¾—
  have target : Q(Prop) := â† getMainTarget

  -- `P` ãŒ `target` ã¨ä¸€è‡´ã—ã¦ã„ã‚‹ãªã‚‰ã€ç¤ºã™ã¹ãã‚´ãƒ¼ãƒ«ã®è¨¼æ˜ãŒå¾—ã‚‰ã‚ŒãŸã“ã¨ã«ãªã‚‹ã€‚
  if (â† isDefEq P target) then
    let goal â† getMainGoal
    goal.assignIfDefEq hp
    return true

  match P with
  | ~q($Q âˆ§ $R) =>
    let hq : Q($Q) := q(And.left $hp)
    let success â† destructAndExpr Q hq -- å†å¸°çš„ã«ãƒã‚§ãƒƒã‚¯
    -- æˆåŠŸã—ã¦ã„ãŸã‚‰æ—©æœŸãƒªã‚¿ãƒ¼ãƒ³
    if success then
      return true

    let hr : Q($R) := q(And.right $hp)
    destructAndExpr R hr -- å†å¸°çš„ã«ãƒã‚§ãƒƒã‚¯
  | _ => return false

@[tactic destructAnd]
def evalDestructAnd : Tactic := fun stx => withMainContext do
  match stx with
  | `(tactic| destruct_and $h) =>
    let h â† getFVarFromUserName h.getId
    let success â† destructAndExpr (â† inferType h) h
    if !success then
      failure
  | _ => throwUnsupportedSyntax

example (a b c d : Prop) (h : a âˆ§ b âˆ§ c âˆ§ d) : a := by
  destruct_and h

example (a b c d : Prop) (h : a âˆ§ b âˆ§ c âˆ§ d) : b := by
  destruct_and h

example (a b c d : Prop) (h : a âˆ§ b âˆ§ c âˆ§ d) : c := by
  destruct_and h

example (a b c d : Prop) (h : a âˆ§ b âˆ§ c âˆ§ d) : d := by
  destruct_and h

example (a b c : Prop) (h : a âˆ§ b âˆ§ c) : a âˆ§ b := by
  constructor <;> destruct_and h

end --#
/- ### `Aâ‚ âˆ§ â‹¯ âˆ§ Aâ‚™` ã¨ã„ã†å½¢ã®å‰æã‚’åˆ†è§£ã—ã¦æ–°ã—ã„ä»®å®šã¨ã—ã¦è¿½åŠ ã™ã‚‹ã‚¿ã‚¯ãƒ†ã‚£ã‚¯

å†å¸°çš„ãªæŒ™å‹•ã‚’ã™ã‚‹ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã®ä¾‹ã¨ã—ã¦ã€ã•ã‚‰ã« `Aâ‚ âˆ§ Aâ‚‚ âˆ§ ... âˆ§ Aâ‚™` ã¨ã„ã†å½¢ã®å‰æã‚’åˆ†è§£ã—ã¦æ–°ã—ã„ä»®å®šã¨ã—ã¦è¿½åŠ ã™ã‚‹ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã‚’å®Ÿè£…ã™ã‚‹ä¾‹ã‚’ç¤ºã—ã¾ã™ã€‚[^cases_and]
-/
section --#

/-- ä»®å®šã«ã‚ã‚‹ `Aâ‚ âˆ§ Aâ‚‚ âˆ§ ... âˆ§ Aâ‚™` ã‚’åˆ†è§£ã™ã‚‹ -/
syntax (name := casesAnd) "cases_and" : tactic

open Lean Elab Tactic Meta Qq Parser Term

/-- å‘½é¡Œ`P`ã¨ãã®è¨¼æ˜é …`hp`ã‚’å—ã‘å–ã‚Šã€`P = Qâ‚ âˆ§ Qâ‚‚ âˆ§ ... âˆ§ Qâ‚™` ã¨ã„ã†å½¢ã ã£ãŸå ´åˆã«ã¯
å„`Qáµ¢`ã¨ãã®è¨¼æ˜é …`hqáµ¢`ã®ãƒªã‚¹ãƒˆã‚’è¿”ã™ã€‚ãã®å½¢ã§ãªã‘ã‚Œã°å˜ã«`[(P, hp)]`ã‚’è¿”ã™ã€‚-/
partial def casesAndAux (P : Q(Prop)) (hp : Q($P)) : TacticM (List ((P : Q(Prop)) Ã— Q($P))) := do
  if (â† inferType hp) != P then
    throwError "å‹ã®ä¸ä¸€è‡´ã‚¨ãƒ©ãƒ¼: {hp} ã¯ {P} ã®è¨¼æ˜ã§ã¯ã‚ã‚Šã¾ã›ã‚“"

  if let ~q($Qâ‚ âˆ§ $Qâ‚‚) := P then
    let hqâ‚ : Q($Qâ‚) := q(And.left $hp)
    let hqâ‚‚ : Q($Qâ‚‚) := q(And.right $hp)
    return (â† casesAndAux Qâ‚ hqâ‚) ++ (â† casesAndAux Qâ‚‚ hqâ‚‚)
  else
    return [âŸ¨P, hpâŸ©]

@[tactic casesAnd]
def evalCasesAnd : Tactic := fun _stx => withMainContext do
  -- ç¾åœ¨ã®ãƒ­ãƒ¼ã‚«ãƒ«ã‚³ãƒ³ãƒ†ã‚­ã‚¹ãƒˆã‚’å–å¾—ã™ã‚‹
  let ctx â† getLCtx

  for (decl : LocalDecl) in ctx do
    -- ãƒ­ãƒ¼ã‚«ãƒ«å®£è¨€ã®ç¨®é¡ãŒãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã§ãªã„å ´åˆã¯ã‚¹ã‚­ãƒƒãƒ—ã™ã‚‹
    if decl.kind != .default then
      continue

    -- `hp : P` (ãŸã ã— `P : Prop`)ã¨ã„ã†ã‚¿ã‚¤ãƒ—ã®ãƒ­ãƒ¼ã‚«ãƒ«å®£è¨€ã«çµã‚Šè¾¼ã‚€
    -- `A : Prop` ã®ã‚ˆã†ãªãƒ­ãƒ¼ã‚«ãƒ«å®£è¨€ã¯é™¤å¤–ã™ã‚‹
    let declType := decl.type
    let declTypeType â† inferType declType
    if ! declTypeType.isProp then
      continue

    -- å®£è¨€ã—ç›´ã™
    have P : Q(Prop) := declType
    have hp : Q($P) := decl.toExpr
    trace[debug] m!"å¯¾è±¡ã¨ãªã‚‹ãƒ­ãƒ¼ã‚«ãƒ«å®£è¨€ã‚’è¦‹ã¤ã‘ã¾ã—ãŸ: {hp} : {P}"

    let proofList â† casesAndAux P hp
    for (âŸ¨Q, hqâŸ©, idx) in proofList.zipIdx do
      let hypName := decl.userName.appendAfter s!"_{idx}"
      trace[debug] m!"æ–°ã—ã„ä»®å®šã‚’è¿½åŠ : {Q}"
      evalTactic <| â† `(tactic| have $(mkIdent hypName) : $(â† Q.toSyntax) := $(â† hq.toSyntax))

-- ãƒ‡ãƒãƒƒã‚°å‡ºåŠ›ã‚’OFFã«ã™ã‚‹
set_option trace.debug false

/-â‹†-//--
trace: a b c d : Prop
h : a âˆ§ (b âˆ§ c) âˆ§ d
h_0 : a
h_1 : b
h_2 : c
h_3 : d
âŠ¢ b âˆ§ d
-/
#guard_msgs in --#
example (a b c d : Prop) (h : a âˆ§ (b âˆ§ c) âˆ§ d) : b âˆ§ d := by
  cases_and
  trace_state -- ç¾åœ¨ã® infoview ã®çŠ¶æ…‹ã‚’è¡¨ç¤º

  constructor <;> assumption

end --#
/- ### exact? ã‚¿ã‚¯ãƒ†ã‚£ã‚¯

ã‚´ãƒ¼ãƒ«ã‚’ç›´æ¥é–‰ã˜ã‚‹ã“ã¨ãŒã§ãã‚‹å®šç†ã‚’æ¢ã™ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ã¨ã—ã¦ [`exact?`](#{root}/Tactic/ExactQuestion.md) ã‚¿ã‚¯ãƒ†ã‚£ã‚¯ãŒã‚ã‚Šã¾ã™ã€‚ã“ã‚Œã«ç›¸å½“ã™ã‚‹ï¼ˆã—ã‹ã—ã‹ãªã‚ŠåŸå§‹çš„ã§ä½æ€§èƒ½ãªï¼‰ã‚‚ã®ã‚’è‡ªå‰ã§å®Ÿè£…ã™ã‚‹ä¾‹ã‚’ç¤ºã—ã¾ã™ã€‚[^exact?]
-/

-- `my_exact?` ã¨ã„ã†ã‚¿ã‚¯ãƒ†ã‚£ãƒƒã‚¯ã®æ§‹æ–‡ã‚’å®šç¾©ã™ã‚‹ï¼ˆæ§‹æ–‡ã¨ã—ã¦ `my_exact?` ã‚’èªè­˜ã•ã›ã‚‹ï¼‰
syntax (name := myExact?) "my_exact?" : tactic

open Lean Elab Tactic in

-- `my_exact?` ã‚¿ã‚¯ãƒ†ã‚£ãƒƒã‚¯ã®å®Ÿè£…ã‚’å®šç¾©ã™ã‚‹
@[tactic myExact?]
def evalMyExact? : Tactic := fun _stx => do
  -- ç¾åœ¨ã®ç’°å¢ƒï¼ˆå®šç†ãªã©ãŒæ ¼ç´ã•ã‚Œã¦ã„ã‚‹ï¼‰ã‚’å–å¾—
  let env â† getEnv

  -- ç’°å¢ƒä¸­ã®å®šæ•°ã‚’å–å¾—ã—ã€ä»¥ä¸‹ã®æ¡ä»¶ã§ãƒ•ã‚£ãƒ«ã‚¿ãƒ¼ã™ã‚‹ï¼š
  -- 1. unsafe ãªå®šæ•°ã§ã¯ãªã„
  -- 2. ç¨®é¡ãŒ axiom ã‹ thmï¼ˆå®šç†ï¼‰ã®ã‚‚ã®
  let theorems : List Name := SMap.toList (Environment.constants env)
    |>.filter (fun (_name, info) => ! ConstantInfo.isUnsafe info)
    |>.filterMap (fun (name, _info) => do
        let kind â† getOriginalConstKind? env name
        match kind with
        | .axiom | .thm => name
        | _ => none
      )

  -- æ¡ä»¶ã‚’æº€ãŸã™å®šç†ã«å¯¾ã—ã¦é †ã«è©¦ã™
  for name in theorems do
    try
      -- åå‰ã‚’æ§‹æ–‡ãƒãƒ¼ãƒ‰ã«å¤‰æ›
      let nameStx := mkIdent name

      -- `apply name <;> assumption` ã‚’æ§‹æ–‡çš„ã«å±•é–‹ãƒ»å®Ÿè¡Œã™ã‚‹
      evalTactic <| â† `(tactic| apply $nameStx <;> assumption)

      -- æˆåŠŸã—ãŸã‚‰ãƒ­ã‚°ã‚’å‡ºåŠ›ã—ã€ã‚¿ã‚¯ãƒ†ã‚£ãƒƒã‚¯ã®å®Ÿè¡Œã‚’çµ‚äº†ã™ã‚‹
      logInfo m!"Applied {name} successfully."
      return

    catch _ =>
      -- å¤±æ•—ã—ã¦ã‚‚ç¶šè¡Œï¼ˆæ¬¡ã®å®šç†ã‚’è©¦ã™ï¼‰
      continue

  -- ã©ã®å®šç†ã‚‚é©ç”¨ã§ããªã‹ã£ãŸå ´åˆã¯ã‚¿ã‚¯ãƒ†ã‚£ãƒƒã‚¯ã¨ã—ã¦å¤±æ•—ã‚’è¿”ã™
  failure

set_option maxHeartbeats 500000 in

-- ä½¿ç”¨ä¾‹
example (x y : Nat) (h : x = y) : y = x := by
  my_exact?

/- [^trivial]: ã“ã®ã‚³ãƒ¼ãƒ‰ä¾‹ã‚’æ›¸ãã«ã‚ãŸã‚Š [lean-tactic-programming-guide](https://github.com/mirefek/lean-tactic-programming-guide) ã‚’å‚è€ƒã«ã—ã¾ã—ãŸã€‚

[^and_constructor]: ã“ã®ã‚³ãƒ¼ãƒ‰ä¾‹ã‚’æ›¸ãã«ã‚ãŸã‚Š [lean-tactic-programming-guide](https://github.com/mirefek/lean-tactic-programming-guide) ã‚’å‚è€ƒã«ã—ã¾ã—ãŸã€‚

[^iff_constructor]: ã“ã®ã‚³ãƒ¼ãƒ‰ä¾‹ã‚’æ›¸ãã«ã‚ãŸã‚Š [Metaprogramming in Lean 4](https://leanprover-community.github.io/lean4-metaprogramming-book/) ã‚’å‚è€ƒã«ã—ã¾ã—ãŸã€‚

[^destruct_and]: ã“ã®ã‚³ãƒ¼ãƒ‰ä¾‹ã‚’æ›¸ãã«ã‚ãŸã‚Š [The Hitchhiker's Guide to Logical Verification](https://github.com/lean-forward/logical_verification_2025) ã‚’å‚è€ƒã«ã—ã¾ã—ãŸã€‚

[^exact?]: ã“ã®ã‚³ãƒ¼ãƒ‰ä¾‹ã‚’æ›¸ãã«ã‚ãŸã‚Š [The Hitchhiker's Guide to Logical Verification](https://github.com/lean-forward/logical_verification_2025) ã‚’å‚è€ƒã«ã—ã¾ã—ãŸã€‚

[^cases_and]: ã“ã®ã‚³ãƒ¼ãƒ‰ä¾‹ã‚’æ›¸ãã«ã‚ãŸã‚Š [The Hitchhiker's Guide to Logical Verification](https://github.com/lean-forward/logical_verification_2025) ã®æ¼”ç¿’å•é¡Œã‚’å‚è€ƒã«ã—ã¾ã—ãŸã€‚
-/
